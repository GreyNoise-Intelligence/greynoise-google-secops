/*
 * Copyright 2025 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
 
rule greynoise_intelligence_inbound_network_traffic_with_asn_context {

  meta:
    author = "GreyNoise Intelligence"
    rule_name = "GreyNoise Intelligence Inbound Network Traffic with ASN Context"
    description = "Detects allowed inbound firewall connections from malicious IPs flagged by GreyNoise with correlated ASN context."
    severity = "High"
    priority = "Critical"
    mitre_attack_tactic = "Initial Access"
    tags = "cloud security, threat intelligence"

  events:
    $network.metadata.event_type = "NETWORK_CONNECTION"
    $network.security_result.action = "ALLOW"
    re.regex($network.metadata.product_name, `(?i).*firewall.*`)
    (
        $network.src.ip = $correlation_ip or
        $network.principal.ip = $correlation_ip
    )

    // GreyNoise entity match
    $greynoise.graph.metadata.event_metadata.base_labels.log_types = "GREYNOISE"
    $greynoise.graph.metadata.product_name = "GreyNoise Intelligence"
    $greynoise.graph.metadata.entity_type = "IP_ADDRESS"
    $greynoise.graph.metadata.threat.threat_verdict = "MALICIOUS"
    $greynoise.graph.entity.ip = $correlation_ip
    (
        re.capture($network.principal.ip_geo_artifact.network.asn, `(\d+)`) = re.capture($greynoise.graph.entity.ip_geo_artifact.network.asn, `(\d+)`)
        or
        re.capture($network.src.ip_geo_artifact.network.asn, `(\d+)`) = re.capture($greynoise.graph.entity.ip_geo_artifact.network.asn, `(\d+)`)
    )

  match:
    $correlation_ip over 1h

  outcome:
    $principal_ip = array_distinct($network.principal.ip)
    $principal_hostname = array_distinct($network.principal.hostname)
    $principal_user_userid = array_distinct($network.principal.user.userid)
    $principal_mac = array_distinct($network.principal.mac)
    
    $source_ip = array_distinct($network.src.ip)
    $source_hostname = array_distinct($network.src.hostname)
    $source_user_userid = array_distinct($network.src.user.userid)
    $source_mac = array_distinct($network.src.mac)

    $target_ip = array_distinct($network.target.ip)
    $target_hostname = array_distinct($network.target.hostname)
    $target_user_userid = array_distinct($network.target.user.userid)
    $target_mac = array_distinct($network.target.mac)

  condition:
    $network and $greynoise
}
